plugins {
    id "com.gradle.plugin-publish" version "0.15.0"
}
//发布到远程gradle plugin portal，获得approval超级慢
pluginBundle {
    vcsUrl = 'https://github.com/JamesfChen/bundles-assembler'
    website = "https://github.com/JamesfChen/bundles-assembler"
    description = "replugin-plugin"
    tags = ['bundles-assembler']
    def ver = "1.0.0"
    plugins {
        pluginifyHost {
            displayName = 'replugin-host-plugin'
            tags = ['bundles-assembler']
            version = ver
        }
        pluginifyParasite {
            displayName = 'replugin-parasite-plugin'
            tags = ['bundles-assembler']
            version = ver
        }
    }
    mavenCoordinates {
        groupId = rootProject.groupId
        artifactId = 'replugin-plugin'
        version = ver
    }
}
apply plugin: 'java'
java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}
apply plugin: 'groovy'
tasks.withType(GroovyCompile) {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
}
String verPath = "${project.projectDir}/src/main/groovy/com/qihoo360/replugin/Constants.groovy"
String verLine = new File(verPath).filterLine { it =~ /def static final VER =/ }
version = "${verLine.split("\"")[1]}" // 版本
//红色醒目打印显示版本号
java.lang.System.err.println "version=${version}"
dependencies {
    compileOnly "com.android.tools.build:gradle:${rootProject.agpVersion}"
    implementation 'org.json:json:20180813'
    implementation 'org.codehaus.groovy:groovy:3.0.9'
    implementation 'com.squareup:javapoet:1.13.0'
    implementation localGroovy()
    implementation 'com.google.gradle:osdetector-gradle-plugin:1.6.0'
    implementation 'org.javassist:javassist:3.18.2-GA'
    implementation 'commons-io:commons-io:2.11.0'
    implementation 'net.dongliu:apk-parser:2.2.0'
    implementation 'com.google.guava:guava:24.1-jre'
    implementation 'commons-codec:commons-codec:1.15'
    implementation project(':base-plugin')
}
apply plugin: "java-gradle-plugin"
gradlePlugin {
    plugins {
        pluginifyHost {
            id = "${rootProject.groupId}.replugin-host-plugin"
            implementationClass = 'com.qihoo360.replugin.host.HostPlugin'
            displayName = "pluginify-host-plugin"
            description = "pluginify-host-plugin"
        }
        pluginifyParasite {
            id = "${rootProject.groupId}.replugin-parasite-plugin"
            implementationClass = 'com.qihoo360.replugin.parasite2.ParasitePlugin'
            displayName = "pluginify-parasite-plugin"
            description = "pluginify-parasite-plugin"
        }
    }
}